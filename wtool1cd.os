#!C:/home/dmpas/workspace/onescript/src/oscript/bin/Debug/oscript.exe -cgi
//#!C:/opt/oscript/bin/oscript.exe -cgi
//#!oscript -cgi

#Использовать tool1cd
#Использовать tempfiles

Перем ПараметрыЗапроса;
Перем ПутьКБазе;

Процедура ПодготовитьДанныеИзВходящегоЗапроса()

	ПараметрыЗапроса = Новый Соответствие;
	Для Каждого мКЗ Из ВебЗапрос.GET Цикл
		
		Если мКЗ.Значение = Неопределено Тогда

			// Возникает в случаях, если путь передан как /some/thing?flag1&flag2&flag3 :
			// Ключ flag1 присутствует, но ВебЗапрос.GET["flag1"] = Неопределено
			ПараметрыЗапроса.Вставить(мКЗ.Ключ, Истина);

		Иначе

			ПараметрыЗапроса.Вставить(мКЗ.Ключ, мКЗ.Значение);

		КонецЕсли

	КонецЦикла;

	ПутьКБазе = ВебЗапрос.ENV["PATH_TRANSLATED"];

КонецПроцедуры

Процедура ОшибкаЗапроса(Знач КодОшибки, Знач ТекстОшибки)

	ВывестиЗаголовок("Status", КодОшибки + " " + ТекстОшибки);
	ЗавершитьРаботу(0);

КонецПроцедуры

Процедура ВыполнитьОперацию(Знач ИмяОперации)

	Если ИмяОперации = "drc" Тогда
		// Отдать версию конфигурации

		Версия = ПараметрыЗапроса.Получить("ver");
		Если Версия = Неопределено Тогда
			Версия = 0;
		Иначе

			Попытка

				Версия = Число(Версия);

			Исключение

				ОшибкаЗапроса("400", "Неверно указана версия хранилища");
				Возврат;

			КонецПопытки;

		КонецЕсли;

		ИмяФайла = ПолучитьИмяВременногоФайла("cf");
		ЧтениеХранилища = Новый ЧтениеХранилищаКонфигурации;
		ЧтениеХранилища.ВыгрузитьВерсиюКонфигурации(ПутьКБазе, ИмяФайла, Версия);
		
		ОтправитьФайл(ИмяФайла, "v" + Версия + ".cf");

		УдалитьФайлы(ИмяФайла);

	ИначеЕсли ИмяОперации = "ex" Тогда

		ИмяТаблицы = ПараметрыЗапроса["name"];
		Если Не ЗначениеЗаполнено(ИмяТаблицы) Тогда
			
			ОшибкаЗапроса("400", "Не указано имя таблицы");
			Возврат;

		КонецЕсли;

		ЧтениеТаблиц = Новый ЧтениеТаблицФайловойБазыДанных;
		ЧтениеТаблиц.ОткрытьФайл(ПутьКБазе);
		Попытка
			
			ФайлыТаблиц = ЧтениеТаблиц.ВыгрузитьТаблицыВXML(ИмяТаблицы);

		Исключение
			
			ОшибкаЗапроса("404", "Таблица не найдена");
			Возврат;

		КонецПопытки;

		ИмяФайлаТаблицы = ФайлыТаблиц[ИмяТаблицы];
		
		ОтправитьФайл(ИмяФайлаТаблицы);

		ЧтениеТаблиц.ЗакрытьФайл();

	КонецЕсли;

КонецПроцедуры

КаталогВременныхФайлов = "C:\temp\"; // TODO: Разобраться, почему каталог временных файлов отправляет нас в C:\Windows

ВывестиЗаголовок("Content-type", "text/plain");
ВывестиЗаголовок("Content-encoding", "utf-8");

ПодготовитьДанныеИзВходящегоЗапроса();

ИзвестныеКлючи = СтрРазделить("ex,drc", ",");

Операция = Неопределено;
Для Каждого мКлюч Из ИзвестныеКлючи Цикл

	Если ПараметрыЗапроса.Получить(мКлюч) = Истина Тогда

		Операция = мКлюч;
		Прервать;

	КонецЕсли;

КонецЦикла;

Если ЗначениеЗаполнено(Операция) Тогда

	ВременныеФайлы.БазовыйКаталог = КаталогВременныхФайлов;
	Попытка
		ВыполнитьОперацию(Операция);
	Исключение
		
		ВывестиЗаголовок("Status", "500 Unknown command");
		Сообщить(ОписаниеОшибки());

	КонецПопытки;

	ВременныеФайлы.Удалить();

Иначе
	ВывестиЗаголовок("Status", "400 Unknown command");
КонецЕсли;


